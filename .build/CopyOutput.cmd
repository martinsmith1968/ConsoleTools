@ECHO OFF

IF "%~1" == "" GOTO :EOF
IF "%~2" == "" GOTO :EOF
IF "%~3" == "" GOTO :EOF

SETLOCAL EnableDelayedExpansion

SET SCRIPTPATH=%~dp0
SET SCRIPTNAME=%~n0
SET SCRIPTFULLFILENAME=%~dpnx0

FOR %%* IN (.) DO SET CURRENTDIR=%%~dpnx*
FOR %%* IN (.) DO SET CURRENTDIRNAME=%%~n*

SET ASSEMBLYNAME=%~1
SET CONFIGURATION=%~2
SET OUTDIR=%~3

ECHO.CURRENTDIR=%CURRENTDIR%
ECHO.SCRIPTPATH=%SCRIPTPATH%

ECHO.ASSEMBLYNAME=%ASSEMBLYNAME%
ECHO.CONFIGURATION=%CONFIGURATION%
ECHO.OUTDIR=%OUTDIR%

CALL :COPYARTIFACT "%CURRENTDIR%\%OUTDIR%\%ASSEMBLYNAME%.dll" "%SCRIPTPATH%\%CONFIGURATION%"
CALL :COPYARTIFACT "%CURRENTDIR%\%OUTDIR%\%ASSEMBLYNAME%.exe" "%SCRIPTPATH%\%CONFIGURATION%"

GOTO :EOF


:COPYARTIFACT
IF "%~1" == "" GOTO :EOF
IF "%~2" == "" GOTO :EOF

ECHO.Checking File: %~1

IF NOT EXIST "%~1" GOTO :EOF

IF NOT EXIST "%~2\*.*" (
    ECHO.Creating: %~2
    MD "%~2"
)

ECHO.Copying: %~1 to %~2
COPY /Y /B /V "%~1" "%~2"

GOTO :EOF
